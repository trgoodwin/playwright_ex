defmodule PlaywrightEx.Connection do
  @moduledoc """
  Stateful, `:gen_statem` based connection to a Playwright node.js server.
  The connection is established via a transport (`PlaywrightEx.PortTransport` or `PlaywrightEx.WebSocketTransport`).

  States:
  - `:pending`: Initial state, waiting for Playwright initialization. Post calls are postponed.
  - `:started`: Playwright is ready, all operations are processed normally.
  """
  @behaviour :gen_statem

  import Kernel, except: [send: 2]

  alias PlaywrightEx.FrameEventRecorder
  alias PlaywrightEx.Serialization

  @timeout_grace_factor 1.5
  @min_genserver_timeout to_timeout(second: 1)

  defstruct config: %{js_logger: nil, transport: {nil, nil}},
            initializers: %{},
            pending_response: %{},
            bindings: %{}

  @doc false
  def child_spec(opts) do
    %{id: __MODULE__, start: {__MODULE__, :start_link, opts}}
  end

  @doc false
  def start_link(opts) do
    opts = Keyword.validate!(opts, [:timeout, :transport, :name, :pg_scope, js_logger: nil])
    timeout = Keyword.fetch!(opts, :timeout)
    name = Keyword.fetch!(opts, :name)

    :gen_statem.start_link({:local, name}, __MODULE__, Map.new(opts), timeout: timeout)
  end

  @doc """
  Subscribe to messages for a guid.
  """
  def subscribe(name, pid \\ self(), guid) do
    :gen_statem.cast(name, {:subscribe, pid, guid})
  end

  @doc """
  Unsubscribe from messages for a guid.
  """
  def unsubscribe(name, pid \\ self(), guid) do
    :gen_statem.cast(name, {:unsubscribe, pid, guid})
  end

  @doc false
  def handle_playwright_msg(name, msg) do
    :gen_statem.cast(name, {:playwright_msg, msg})
  end

  @doc """
  Post a message and await the response.
  Wait for an additional grace period after the playwright timeout.
  """
  def send(name, %{guid: _, method: _} = msg, timeout) when is_integer(timeout) do
    msg =
      msg
      |> Enum.into(%{params: %{}, metadata: %{}})
      |> put_in([:params, :timeout], timeout)
      |> Map.put_new_lazy(:id, fn -> System.unique_integer([:positive, :monotonic]) end)

    call_timeout = max(@min_genserver_timeout, round(timeout * @timeout_grace_factor))

    :gen_statem.call(name, {:send, msg}, call_timeout)
  end

  @doc """
  Get the initializer data for a channel.
  """
  def initializer!(name, guid) do
    :gen_statem.call(name, {:initializer, guid})
  end

  @doc """
  Returns `true` if the connection uses a remote (WebSocket) transport.
  """
  def remote?(name) do
    :gen_statem.call(name, :remote?)
  end

  @doc """
  Register a process to receive binding calls for the given binding name.

  When JavaScript calls the exposed binding, `pid` receives
  `{:binding_call, %{name: String.t(), args: list(), frame: String.t()}}`.
  """
  def register_binding(name, pid \\ self(), binding_name) do
    :gen_statem.cast(name, {:register_binding, pid, binding_name})
  end

  # Internal

  @impl :gen_statem
  def callback_mode, do: :state_functions

  @impl :gen_statem
  def init(config) do
    %{timeout: timeout, transport: transport} = config

    post(transport, %{
      guid: "",
      method: :initialize,
      params: %{sdk_language: :javascript, timeout: timeout},
      metadata: %{}
    })

    {:ok, :pending, %__MODULE__{config: config}}
  end

  defp post({transport_module, transport_name}, msg) do
    transport_module.post(transport_name, msg)
  end

  @doc false
  def pending(:cast, {:playwright_msg, %{method: :__create__, params: %{guid: "Playwright"}} = msg}, data) do
    {:next_state, :started, handle_create(data, msg)}
  end

  def pending(:cast, _msg, _data), do: {:keep_state_and_data, [:postpone]}
  def pending({:call, _from}, _msg, _data), do: {:keep_state_and_data, [:postpone]}

  @doc false
  def started({:call, from}, {:send, msg}, data) do
    post(data.config.transport, msg)
    {:keep_state, put_in(data.pending_response[msg.id], from)}
  end

  def started({:call, from}, {:initializer, guid}, data) do
    {:keep_state_and_data, [{:reply, from, Map.fetch!(data.initializers, guid)}]}
  end

  def started({:call, from}, :remote?, data) do
    {transport_module, _} = data.config.transport
    {:keep_state_and_data, [{:reply, from, transport_module != PlaywrightEx.PortTransport}]}
  end

  def started(:cast, {:subscribe, recipient, guid}, data) do
    group = pg_group(guid)

    if recipient in :pg.get_members(data.config.pg_scope, group) do
      :ok
    else
      :ok = :pg.join(data.config.pg_scope, group, recipient)
    end

    :keep_state_and_data
  end

  def started(:cast, {:unsubscribe, recipient, guid}, data) do
    _ = :pg.leave(data.config.pg_scope, pg_group(guid), recipient)
    :keep_state_and_data
  end

  def started(:cast, {:playwright_msg, %{method: :page_error} = msg}, data) do
    if module = data.config.js_logger do
      module.log(:error, msg.params.error, msg)
    end

    :keep_state_and_data
  end

  def started(:cast, {:playwright_msg, %{method: :console} = msg}, data) do
    if module = data.config.js_logger do
      level = log_level_from_js(msg[:params][:type])
      module.log(level, msg.params.text, msg)
    end

    :keep_state_and_data
  end

  def started(
        :cast,
        {:playwright_msg,
         %{method: :__create__, params: %{type: "BindingCall", guid: bc_guid, initializer: %{name: name} = init}}},
        data
      ) do
    args = Map.get(init, :args, [])

    case Map.fetch(data.bindings, name) do
      {:ok, pid} ->
        frame_guid = get_in(init, [:frame, :guid])

        Kernel.send(pid, {:binding_call, %{name: name, args: Serialization.deserialize_arg(args), frame: frame_guid}})

        post(data.config.transport, %{
          guid: bc_guid,
          method: :resolve,
          params: %{result: Serialization.serialize_arg(nil)},
          metadata: %{}
        })

      :error ->
        :ok
    end

    :keep_state_and_data
  end

  def started(:cast, {:register_binding, pid, binding_name}, data) do
    {:keep_state, %{data | bindings: Map.put(data.bindings, binding_name, pid)}}
  end

  def started(:cast, {:playwright_msg, msg}, data) when is_map_key(data.pending_response, msg.id) do
    {from, pending_response} = Map.pop(data.pending_response, msg.id)
    :gen_statem.reply(from, msg)

    {:keep_state, %{data | pending_response: pending_response}}
  end

  def started(:cast, {:playwright_msg, msg}, data) do
    {:keep_state,
     data |> handle_create(msg) |> maybe_start_frame_event_recorder(msg) |> notify_subscribers(msg) |> handle_dispose(msg)}
  end

  defp handle_create(data, %{method: :__create__} = msg) do
    put_in(data.initializers[msg.params.guid], msg.params.initializer)
  end

  defp handle_create(data, _msg), do: data

  defp maybe_start_frame_event_recorder(data, %{
         method: :__create__,
         params: %{guid: guid, initializer: %{url: _url, load_states: _load_states} = initializer}
       }) do
    _ = FrameEventRecorder.ensure_started(data.config.name, guid, initializer)
    data
  end

  defp maybe_start_frame_event_recorder(data, %{method: :__create__}) do
    data
  end

  defp maybe_start_frame_event_recorder(data, _msg), do: data

  defp handle_dispose(data, %{method: :__dispose__} = msg) do
    data
    |> Map.update!(:initializers, &Map.delete(&1, msg.guid))
    |> stop_disposed_frame_event_recorder(msg.guid)
    |> clear_disposed_guid_subscribers(msg.guid)
  end

  defp handle_dispose(data, _msg), do: data

  defp notify_subscribers(data, %{guid: guid} = msg) do
    for pid <- :pg.get_members(data.config.pg_scope, pg_group(guid)) do
      Kernel.send(pid, {:playwright_msg, msg})
    end

    data
  end

  defp notify_subscribers(data, _msg), do: data

  defp pg_group(guid), do: {:guid, guid}

  defp clear_disposed_guid_subscribers(data, guid) do
    group = pg_group(guid)

    for pid <- :pg.get_local_members(data.config.pg_scope, group) do
      _ = :pg.leave(data.config.pg_scope, group, pid)
    end

    data
  end

  defp stop_disposed_frame_event_recorder(data, guid) do
    _ = FrameEventRecorder.terminate_frame(data.config.name, guid)
    data
  end

  defp log_level_from_js("error"), do: :error
  defp log_level_from_js("debug"), do: :debug
  defp log_level_from_js(_), do: :info
end
